def verDivider = version.indexOf("-")
def verPrefix = version
def verSuffix = ""

if (verDivider > 0) {
    verPrefix = version.substring(0, verDivider)
    verSuffix = version.substring(verDivider+1)
}
version = "$verPrefix-ES-$version_elasticsearch-$verSuffix"

println("kepling elasticsearch kotlinscript lib version will be $version")

dependencies {
    compile project(":keplin-util")
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-compiler-embeddable', version: version_kotlin
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-daemon-client', version: version_kotlin
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8', version: version_kotlin

    compile group: 'org.elasticsearch', name: 'elasticsearch', version: version_elasticsearch

    // temporarily, we need to just link with submodule to the files and parse them ourselves
    compile (group: 'org.codelibs.elasticsearch.module', name: 'lang-painless', version: version_elasticsearch) {
        exclude group: 'org.antlr'
        //    exclude group: 'org.ow2.asm'
    }

    testCompile group: 'org.elasticsearch.test', name: 'framework', version: version_elasticsearch
    testRuntime group: 'org.apache.logging.log4j', name: 'log4j-core', version: version_log4j
}

configurations.all {
    // these are in the embeddable compiler already, to avoid jarhell check we cannot have two copies
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect'
    // this is a problem, we need it excluded from Kotlin compiler embeddable so we can use the ES version instead, because this won't work in real life:
    exclude group: 'net.java.dev.jna', module: 'jna'
}
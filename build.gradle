import org.gradle.plugins.ide.idea.model.IdeaModel
import org.gradle.plugins.ide.idea.model.IdeaProject

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: version_kotlin
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'idea'
    apply plugin: 'com.bmuschko.nexus'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {

    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.incremental = true
        options.compilerArgs << '-Xlint:unchecked'
    }

    IdeaModel idea = project.idea
    IdeaProject ideaProject = idea.getProject()

    if (ideaProject != null) {
        ideaProject.setJdkName(project.targetCompatibility.getName())
        ideaProject.setLanguageLevel(project.sourceCompatibility.getName())
        if (ideaProject.hasProperty("vcs")) {
            ideaProject.setVcs('Git')
        }
    }

    idea.module {
        downloadJavadoc = true
        downloadSources = true
    }

    task jars() {
        dependsOn build
    }

    task subRelease() {
        dependsOn jars, uploadArchives
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = 1.8
        }
    }

    modifyPom {
        //noinspection GroovyAssignabilityCheck
        project {
            name 'Keplin for Kotlin'
            description 'Scripting and REPLs for Kotlin'
            url 'https://github.com/kohesive/keplin'
            inceptionYear '2017'

            scm {
                url 'https://github.com/kohesive/keplin'
                connection 'scm:https://github.com/kohesive/keplin'
                developerConnection 'scm:git@github.com:kohesive/keplin.git'
            }

            licenses {
                license {
                    name 'MIT'
                    url 'https://github.com/kohesive/keplin/blob/master/LICENSE'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'apatrida'
                    name 'Jayson Minard'
                    email 'jayson.minard@gmail.com'
                }
            }
        }

    }

    nexus {
        sign = true
    }

    bintray {
        user = "$bintrayUser"
        key = "$bintrayKey"
        pkg {
            repo = 'kohesive'
            name = project.name
            licenses = ['MIT']
            vcsUrl = 'scm:git:git@github.com:kohesive/keplin.git'

            version {
                name = project.version
                desc = 'n/a'
                released = new Date()
            }
            publicDownloadNumbers = false
            configurations = ['archives']
        }
        publish = true
        dryRun = false
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = "$version_gradle"
}

task jars() {
    dependsOn subprojects.jars
}

task release() {
    dependsOn subprojects.subRelease
}


defaultTasks 'clean', 'jars'
